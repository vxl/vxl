# This is core/tests/CMakeLists.txt

# The tests here are general vxl configuration tests, and should not
# depend on any libraries other than vcl and testlib.

# header to tell test_build_info values of vxl_BUILD_DIR, BUILD_NAME,
# and SITE from CMake
configure_file( ${CMAKE_CURRENT_SOURCE_DIR}/test_build_info.h.in
                ${CMAKE_CURRENT_BINARY_DIR}/test_build_info.h
                @ONLY IMMEDIATE)

# needed for generated test_build_info.h
include_directories( ${CMAKE_CURRENT_BINARY_DIR} )

# these defines are set (possibly) so the core_test_config test can
# report whether CMake has set these CMake variables
if( UNIX )
  add_definitions( -DCMAKE_DEFINES_VARIABLE_UNIX )
endif( UNIX )
if( CYGWIN )
  add_definitions( -DCMAKE_DEFINES_VARIABLE_CYGWIN )
endif( CYGWIN )
if( MINGW )
  add_definitions( -DCMAKE_DEFINES_VARIABLE_MINGW )
endif( MINGW )
if( WIN32 )
  add_definitions( -DCMAKE_DEFINES_VARIABLE_WIN32 )
endif( WIN32 )
if( APPLE )
  add_definitions( -DCMAKE_DEFINES_VARIABLE_APPLE )
endif( APPLE )

add_executable( core_test_all
  test_driver.cxx
  test_config.cxx
  test_build_info.cxx test_build_info.h.in
)
target_link_libraries( core_test_all testlib vcl )

add_executable( vxl_test_include test_include.cxx )

add_test( NAME core_test_config COMMAND $<TARGET_FILE:core_test_all> test_config )
add_test( NAME core_test_build_info COMMAND $<TARGET_FILE:core_test_all> test_build_info )

# a simple Dart notes file, in case test_build_info fails
if( COMMAND WRITE_FILE )
  write_file(
    "${vxl_BINARY_DIR}/Testing/HTML/TestingResults/Sites/${SITE}/${BUILDNAME}/BuildNameNotes.xml"
    "<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n"
    "<Site BuildName=\"${BUILDNAME}\" Name=\"${SITE}\">\n"
    "<BuildNameNotes>\n"
    "<Note>\n"
    "<DateTime>unset</DateTime>\n"
    "<Text>\n"
    "The full test_build_info did not run.\n"
    "Here is some basic information:\n"
    "vxl_SOURCE_DIR = ${vxl_SOURCE_DIR}\n"
    "vxl_BINARY_DIR = ${vxl_BINARY_DIR}\n"
    "CMAKE_C_COMPILER = ${CMAKE_C_COMPILER}\n"
    "CMAKE_C_FLAGS = ${CMAKE_C_FLAGS}\n"
    "CMAKE_CXX_COMPILER = ${CMAKE_CXX_COMPILER}\n"
    "CMAKE_CXX_FLAGS = ${CMAKE_CXX_FLAGS}\n"
    "CMAKE_SYSTEM = ${CMAKE_SYSTEM}\n"
    "CMAKE_MAKE_PROGRAM = ${CMAKE_MAKE_PROGRAM}\n"
    "</Text>\n"
    "</Note>\n"
    "</BuildNameNotes>\n"
    "</Site>\n"
  )
endif( COMMAND WRITE_FILE )

add_executable( vxl_echo vxl_echo.cxx )
target_link_libraries( vxl_echo vcl )


# Deal with unavailablility of file(TO_CMAKE_PATH ...)
set( MYMAKECOMMAND ${MAKECOMMAND})

if(CMAKE_MAJOR_VERSION GREATER 2)
 file(TO_CMAKE_PATH ${MAKECOMMAND} MYMAKECOMMAND)
endif(CMAKE_MAJOR_VERSION GREATER 2)

if(CMAKE_MAJOR_VERSION EQUAL 2)
  if(CMAKE_MINOR_VERSION  GREATER 3)
     file(TO_CMAKE_PATH ${MAKECOMMAND} MYMAKECOMMAND)
  endif(CMAKE_MINOR_VERSION  GREATER 3)
endif(CMAKE_MAJOR_VERSION EQUAL 2)


add_test( NAME cmake_config COMMAND $<TARGET_FILE:vxl_echo> nl
          MAKECOMMAND: ${MYMAKECOMMAND} nl
          CMAKE_CXX_FLAGS: ${CMAKE_CXX_FLAGS} nl
          CMAKE_C_FLAGS: ${CMAKE_C_FLAGS} nl
          VXL_BUILD_SHARED_LIBS: ${VXL_BUILD_SHARED_LIBS} nl
          CMAKE_EXE_LINKER_FLAGS: ${CMAKE_EXE_LINKER_FLAGS} nl
          CMAKE_MODULE_LINKER_FLAGS: ${CMAKE_MODULE_LINKER_FLAGS} nl
          CMAKE_SHARED_LINKER_FLAGS: ${CMAKE_SHARED_LINKER_FLAGS} nl
          VXL_USE_CMAKE_CONFIGURE: ${VXL_USE_CMAKE_CONFIGURE} nl
          )
